# Maintainer: Kowabunga
# Contributor: Kowabunga
# Contributor: yochananmarqos
# Contributor: bpierre
# Contributor: PedroHLC
# Contributor: rodrigo21

pkgname='lib32-vkpost'
pkgver=0.4.6
pkgrel=3
pkgdesc='A Vulkan Post Processing Layer For Linux (32-bit). Next Gen vkBasalt.'
arch=('x86_64')
url='https://github.com/KowabungaOfficial/vkPost'
license=('Zlib')
depends=('lib32-glibc' 'lib32-gcc-libs' 'lib32-libx11')
makedepends=('glslang' 'meson' 'ninja' 'spirv-headers' 'vulkan-headers')
optdepends=('reshade-shaders-git: collection of shaders to use with vkPost'
            'vkpost: sample config at /usr/share/vkPost/vkPost.conf.example')
source=("${url}/releases/download/v${pkgver}/vkPost-${pkgver}.tar.gz")
b2sums=('616d9166206a14911c26d836a914f1914de831293952fe4064569df125a3c34bed280582de25deeb610f73924dd1629e3b7ad18c334763608348752ab331adc9')

prepare() {
  cd "${srcdir}/vkPost-${pkgver}"
  sed -i 's|/path/to/reshade-shaders/Textures|/opt/reshade/textures|g' \
    "config/vkPost.conf"
  sed -i 's|/path/to/reshade-shaders/Shaders|/opt/reshade/shaders|g' \
    "config/vkPost.conf"
}

build() {
  cd "${srcdir}/vkPost-${pkgver}"

  ASFLAGS+=" --32"
  CFLAGS+=" -m32"
  CXXFLAGS+=" -m32"
  LDFLAGS+=" -m32"
  PKG_CONFIG_PATH=/usr/lib32/pkgconfig \
  arch-meson \
    --buildtype=release \
    --libdir=lib32 \
    -Dwith_json=true \
    build
  ninja -C build
}

package() {
  cd "${srcdir}/vkPost-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C build install
  mv "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkPost.json" "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkPost.x86.json"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
