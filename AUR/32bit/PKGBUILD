# Maintainer: KowabungaOfficial
# contributors: KowabungaOfficial, yochananmarqos, bpierre, PedroHLC, rodrigo21
pkgname='lib32-vkpost'
pkgver=0.4.5
pkgrel=2
pkgdesc='A Vulkan Post Processing Layer For Linux (32-bit). Next Gen vkBasalt.'
arch=('x86_64')
url='https://github.com/KowabungaOfficial/vkPost'
license=('zlib')
depends=('lib32-glibc' 'lib32-gcc-libs' 'lib32-libx11' 'vkpost')
makedepends=('meson' 'ninja' 'glslang' 'spirv-headers' 'vulkan-headers')
optdepends=('reshade-shaders-git')
source=("${url}/releases/download/v${pkgver}/vkPost-${pkgver}.tar.gz")
sha256sums=('81995306af4708c141515cafdf5b8bafa6e1bcd4ce7ae151de9d4e7bf9c9dfa7')

prepare() {
  cd "${srcdir}/vkPost-${pkgver}"
  sed -i 's|/path/to/reshade-shaders/Textures|/opt/reshade/textures|g' \
    "config/vkPost.conf"
  sed -i 's|/path/to/reshade-shaders/Shaders|/opt/reshade/shaders|g' \
    "config/vkPost.conf"
}

build() {
  cd ${srcdir}/vkPost-${pkgver}

  ASFLAGS+=" --32" \
  CFLAGS+=" -m32" \
  CXXFLAGS+=" -m32" \
  LDFLAGS+=" -m32"
  PKG_CONFIG_PATH=/usr/lib32/pkgconfig \
  arch-meson \
    --buildtype=release \
    --libdir=lib32 \
    -Dwith_json=true \
    build
  ninja -C build
}

package() {
  cd ${srcdir}/vkPost-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install
  mv "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkPost.json" "${pkgdir}/usr/share/vulkan/implicit_layer.d/vkpost.x86.json"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
