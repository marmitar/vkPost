# Maintainer: KowabungaOfficial
# contributors: KowabungaOfficial, yochananmarqos, bpierre, PedroHLC, rodrigo21
pkgname='vkpost'
pkgver=0.4.4
pkgrel=2
pkgdesc='A Vulkan Post Processing Layer For Linux. Next Gen vkBasalt.'
arch=('x86_64')
url='https://github.com/KowabungaOfficial/vkPost'
license=('zlib')
depends=('gcc-libs' 'glslang' 'libx11')
makedepends=('meson' 'ninja' 'spirv-headers' 'vulkan-headers')
optdepends=('reshade-shaders-git')
source=("${url}/releases/download/v${pkgver}/vkPost-${pkgver}.tar.gz"
        "vkpost-gcc13-fix.patch::https://github.com/KowabungaOfficial/vkPost/blob/main/AUR/vkpost-gcc13-fix.patch")
sha256sums=('15cbac85be0f3ec811aeb5496d7966a4ee96b6031ad3da510f28ce9541ba6860'
            '97e7fc594261356670580d4714a461e1caf954bcb5994d4b03ea788855d16707')
install=vkpost.install

prepare() {
  cd ${srcdir}/vkPost-${pkgver}
  patch -Np1 -i ../vkpost-gcc13-fix.patch
  sed -i 's|/path/to/reshade-shaders/Textures|/opt/reshade/textures|g' \
    "config/vkPost.conf"
  sed -i 's|/path/to/reshade-shaders/Shaders|/opt/reshade/shaders|g' \
    "config/vkPost.conf"
}

build() {
  cd ${srcdir}/vkPost-${pkgver}

  arch-meson \
    --buildtype=release \
    build
  ninja -C build
}

package() {
  cd ${srcdir}/vkPost-${pkgver}

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 config/vkPost.conf "${pkgdir}/usr/share/vkPost/vkPost.conf.example"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
