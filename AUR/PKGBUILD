# Maintainer: KowabungaOfficial
# contributors: KowabungaOfficial, yochananmarqos, bpierre, PedroHLC, rodrigo21
pkgname='vkpost'
pkgver=0.4.5
pkgrel=2
pkgdesc='A Vulkan Post Processing Layer For Linux. Next Gen vkBasalt.'
arch=('x86_64')
url='https://github.com/KowabungaOfficial/vkPost'
license=('zlib')
depends=('gcc-libs' 'glslang' 'libx11')
makedepends=('meson' 'ninja' 'spirv-headers' 'vulkan-headers')
optdepends=('reshade-shaders-git')
source=("${url}/releases/download/v${pkgver}/vkPost-${pkgver}.tar.gz")
sha256sums=('d57fb8034420ba729249df89f7966f69ca5c8e47a160b1ec414b0cf7a494e91e')
install=vkpost.install

prepare() {
  cd "${srcdir}/vkPost-${pkgver}"
  sed -i 's|/path/to/reshade-shaders/Textures|/opt/reshade/textures|g' \
    "config/vkPost.conf"
  sed -i 's|/path/to/reshade-shaders/Shaders|/opt/reshade/shaders|g' \
    "config/vkPost.conf"
}

build() {
  cd "${srcdir}/vkPost-${pkgver}"

  arch-meson \
    --buildtype=release \
    build
  ninja -C build
}

package() {
  cd "${srcdir}/vkPost-${pkgver}"

  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 config/vkPost.conf "${pkgdir}/usr/share/vkPost/vkPost.conf.example"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
